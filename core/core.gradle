buildscript {
  repositories {
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath 'se.transmode.gradle:gradle-docker:1.2'
  }
}

apply plugin: 'application'
apply plugin: 'docker'

mainClassName = 'io.vertx.core.Launcher'
def mainVerticle = "service:${group}.${project.name}"

applicationDefaultJvmArgs = [ 
  '-Dvertx.logger-delegate-factory-class-name=io.vertx.core.logging.SLF4JLogDelegateFactory',
  '-Dvertx.metrics.options.enabled=true'
]

run {
  args = ['run', mainVerticle]
}

startScripts {
  applicationName = 'service'
  unixStartScriptGenerator.template = resources.text.fromFile('etc/customUnixStartScript.txt')
}

task buildDocker( type: Docker, dependsOn: [startScripts,distTar], group: 'Distribution' ) {
  push = Boolean.getBoolean("docker.push")
  doFirst {
    def buildNumber = System.getenv("BUILD_NUMBER")
    baseImage = 'java:8'
    tagVersion = buildNumber ? "${project.version}-${buildNumber}" : project.version
    registry = 'nineconnections'
    addFile distTar.archivePath, "/srv/"    
    setEnvironment 'SERVICE_HOME', "/srv/${distTar.archiveName}" - ".${distTar.extension}"
    setEnvironment 'PATH', '$SERVICE_HOME/bin:$PATH'
    workingDir '$SERVICE_HOME'
    entryPoint = [ startScripts.applicationName, 'run', mainVerticle ]
  }
}

build.finalizedBy buildDocker

dependencies {
  compile project(':api')
  compile 'org.apache.kafka:kafka-clients:1.0.1'
  compile 'org.apache.kafka:kafka-streams:1.0.1'
  compile 'nl.kii.xtend.cqrses:cqrses-core:0.1.0'

  testCompile 'org.apache.zookeeper:zookeeper:3.4.11'
  testCompile 'org.apache.kafka:kafka_2.11:1.0.1'
  testCompile 'org.apache.kafka:kafka_2.11:1.0.1:test'
  testCompile 'org.apache.kafka:kafka-clients:1.0.1:test'
  testCompile 'org.apache.curator:curator-test:3.2.1'  
}